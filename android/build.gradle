group 'com.tagcommander.tc_serverside_plugin'

def getPubspecVersion() {

    def pubspec = new File(projectDir.parentFile, "pubspec.yaml")
    if (!pubspec.exists()) {
        throw new GradleException("pubspec.yaml not found at ${pubspec.absolutePath}")
    }
    def text = pubspec.getText("UTF-8")
    // Matches e.g. 1.2.3, 1.2.3+4, 1.2.3-dev.1, etc.
    def m = (text =~ /(?m)^\s*version:\s*([0-9]+\.[0-9]+\.[0-9]+(?:[+-][0-9A-Za-z\.\-]+)?)\s*$/)
    if (!m.find()) {
        throw new GradleException("version field not found in pubspec.yaml")
    }
    return m.group(1)
}

ext {
    PLUGIN_VERSION = getPubspecVersion()
}

version PLUGIN_VERSION

buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdk 35
    namespace 'com.tagcommander.tc_serverside_plugin'


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdkVersion 21
    }
}

dependencies{
    //Commanders Act SDK dependencies
    api 'com.tagcommander.lib:core:5.4.8'
    api 'com.tagcommander.lib:ServerSide:5.5.8'
    api 'com.google.code.gson:gson:2.10.1'
}
